## Contract Example
This is a contract example for a ticket selling. There are basically state variables and functions with a contract and all of them would be deployed in the block chain and the contract itself is also an account.  
Because crypto currency will be transferred through contract, it is always important to double check security of the contract. There are several patterns help to secure the contract.   
### Re-entrancy Problem and condition-effect-interaction pattern
Check method `refund` in example contract. This is to prevent a user to get multiple fnuds before varaibles modified. 
### Gas limit problem and Withdrawl Pattern
Check difference between method `refundAll` and `buyerRefundTicket`in exmaple contract. In method `refundAll`, it is supposed to iterate all registrants and refund one by one. The problem of the method is that registrant might be another contract account, and all gas would be used up and fund for left registrants.
### complete codes
```solidity
pragma solidity ^0.4.18;

contract EventRegistrant {

	struct Registrant {
		uint amount;
		uint numTickets;
		string email;
		address buyerAddr;
	}

	address public owner;
	uint public numTicketsSold;
	uint public quota;
	uint public price;
	uint public numRegistrants;
	bool public canceled;

	/*
	reason I have 2 mappings:
	There is not "length" for a mapping, because a mapping is not suppose to be fixed.
	As a result, iteration of a mapping is not recommended. In this scenario, usually one mapping to record index 
	and the other mapping for details.
	
	"registrantsIndex" is to record index of paid addr. 
	1. check if an addr paid
	Because default value in solidity is zero, address did not pay if the index == 0 (the leastest 1) 
	2. get all paid addr
	With varaible "numRegistrants", this mapping provides a list of paid addrs. 
	
	"registrantsDetail" is to save details for addrs. 
	In this mapping, details for a registrant can be fetched by index.
	*/
	mapping (address => uint) registrantsIndex;
	mapping (uint => Registrant) registrantsDetail;
	
	
	event Deposit (address _from, uint _amount);
	event Refund (address _to, uint _amount);

	//Modifiers to define following functions
	//character "_" means this is a modifier
	modifier onlyOwner(){
		require(msg.sender == owner);
		_;
	}
	
	modifier soldOut(){
		require(numTicketsSold < quota);
		_;
	}

	modifier eventCanceled(){
		require(canceled);
		_;
	}

	function EventRegistrant(uint _quota, uint _price) public {
		owner = msg.sender;
		numTicketsSold = 0;
		quota = _quota;
		price = _price;
	}

	function buyTicket(string email, uint numTickets) soldOut public payable {
		//condiftion check.
		uint totalAmount = price * numTickets;
		require(msg.value > totalAmount);
		numTicketsSold += numTickets;
		
		uint index = 0;
		//check if the address paid before
		if (registrantsIndex[msg.sender] != 0) {
			//if index for the user is not zero, buyer paid before
			index = registrantsIndex[msg.sender];
			registrantsDetail[index].amount += totalAmount;
			registrantsDetail[index].numTickets += numTickets;
		} else {
			//index is zero, buyer has not paid before
			//add buyer into index first and then save details.
			//please be attenion that like java and C#, ++variable is difference from variable++
			index = ++numRegistrants;
			registrantsIndex[msg.sender] = index;
			registrantsDetail[index].amount = totalAmount;
			registrantsDetail[index].numTickets = numTickets;
			registrantsDetail[index].email = email;
			registrantsDetail[index].buyerAddr = msg.sender;
		}
		
		//refund the extra fund. 
		if (msg.value > totalAmount) {
			uint refundAmount = msg.value - totalAmount;
			require(msg.sender.send(refundAmount));
		}
		
		Deposit(msg.sender, msg.value);
	}
	
	//this is an example of condition - effect - interaction 
	//the purpose is to prevent re-entrancy. If phase 3 is executed before phase 2, a user, in theory, is able to
	//send mutiple transactions to get multiple funds before varialbe "amount" and "numTickets" is not modified.
	function refundTickets(address buyer) onlyOwner public {
		//Phase 1: check condition to see if the transaction legal
		require(registrantsIndex[buyer] != 0);
		uint index = registrantsIndex[buyer];
		require(registrantsDetail[index].amount > 0);
		require(registrantsDetail[index].numTickets > 0);
		
		//Phase 2: take the effect of variables, so that buyer cannot be refunded multiple times
		uint amountToRefund = registrantsDetail[index].amount;
		uint numTicketsToRefund = registrantsDetail[index].numTickets;
		registrantsDetail[index].amount = 0;
		registrantsDetail[index].numTickets = 0;
		numTicketsSold -= numTicketsToRefund;

		//Phase 3: make the interaction to refund the buyer
		require(buyer.send(amountToRefund));
		Refund(buyer, amountToRefund);
	}

	//use a for-loop to give back fund to user, but this method may be attacked and all gas would be used up.
	function refundAll() onlyOwner public {
		for (uint i = 1; i <= numRegistrants; i++) {
			uint amountToRefund = registrantsDetail[i].amount;
			uint numTickets = registrantsDetail[i].numTickets;
			address buyer = registrantsDetail[i].buyerAddr;

			require(amountToRefund > 0);
			require(numTickets > 0);

			registrantsDetail[i].amount = 0;
			registrantsDetail[i].numTickets = 0;
			numTicketsSold -= numTickets;

			require(buyer.send(amountToRefund));
			Refund(buyer, amountToRefund);
		}
	}

	//withdrawl pattern. Mark event canceled and make user enable to refund tickets
	//this allows all buyers to get refund by themselves.
	function cancelEvent() onlyOwner public {
		canceled = true;
	}

	function buyerRefundTicket() eventCanceled public {
		uint index = registrantsIndex[msg.sender];
		require(index != 0);
		require(registrantsDetail[index].amount > 0);
		require(registrantsDetail[index].numTickets > 0);

		uint amountToRefund = registrantsDetail[index].amount;
		uint numTicketsToRefund = registrantsDetail[index].numTickets;
		registrantsDetail[index].amount = 0;
		registrantsDetail[index].numTickets = 0;
		numTicketsSold -= numTicketsToRefund;

		require(msg.sender.send(amountToRefund));
		Refund(msg.sender, amountToRefund);
	}


	function getRegistrantIndex(address buyer) public view returns (uint) {
		return registrantsIndex[buyer];
	}

	function getRegistrantsAmountPaid(address buyer) public view returns (uint) {
		require(registrantsIndex[buyer] != 0);
		return registrantsDetail[registrantsIndex[buyer]].amount;
	}

	function withdrawFunds() onlyOwner public {
		require(owner.send(this.balance));
	}

	//destructor, to delete the contract when there is no need
	function kill() onlyOwner public {
		selfdestruct(owner);
	}
}
```
